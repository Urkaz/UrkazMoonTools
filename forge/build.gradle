plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    //modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"

    // Enhanced Celestials
    modImplementation "curse.maven:enhanced-celestials-forge-${ec_forge_projectid}:${ec_forge_fileids}"
    modRuntimeOnly "curse.maven:corgilib-693313:${corgilib_forge_fileids}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand  "version": project.version,
                "mc_version": project.minecraft_version,
                "cloth_config_version": project.cloth_config_version,
                "architectury_version": project.architectury_version,
                "enhanced_celestials_version": project.enhanced_celestials_version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar

    classifier "${minecraft_version}-forge"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

/*publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}*/

unifiedPublishing {
    project {
        displayName = "[Forge $rootProject.minecraft_version] $project.mod_name v$project.version"
        version = "$project.version"
        releaseType = "release"
        gameVersions = ["1.20.1"]
        gameLoaders = ["forge"]

        relations {
            depends {
                curseforge = "cloth-config"
                modrinth = "cloth-config"
            }
            optional {
                curseforge = "enhanced-celestials"
                modrinth = "enhanced-celestials"
            }
        }

        mainPublication remapJar

        if (project.hasProperty("cuseforge_api_key") || System.getenv("cuseforge_api_key") != null) {
            curseforge {
                token = project.hasProperty("cuseforge_api_key") ? project.property("cuseforge_api_key") : System.getenv("cuseforge_api_key")
                id = "${project.curseforge_id}"
                gameVersions.addAll "Java 17"
            }
        }

        if (project.hasProperty("modrinth_api_key") || System.getenv("modrinth_api_key") != null) {
            modrinth {
                token = project.hasProperty("modrinth_api_key") ? project.property("modrinth_api_key") : System.getenv("modrinth_api_key")
                id = "${project.modrinth_id}"
            }
        }
    }
}

rootProject.tasks.getByName("modPublish").dependsOn tasks.getByName("publishUnified")